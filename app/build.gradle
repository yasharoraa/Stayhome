apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'


android {
    compileSdkVersion 30
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.stayhome.user"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 6
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs{
        release {
            storeFile file('/home/yash/StayHomeKeyStores/styhmeuser.jks')
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSSWORD
        }
    }



    buildTypes {
        debug {
            def getDevHost = { ->
                def defaultDevHost = InetAddress.getLocalHost()
                        .getCanonicalHostName()

                return NetworkInterface.getNetworkInterfaces()
                        .findResult(defaultDevHost) {
                            def ifOk = it.isUp() &&
                                    !it.isVirtual() &&
                                    !it.isPointToPoint() &&
                                    !it.isLoopback() &&
                                    !it.getName().startsWith("br-") //

                            if (ifOk)
                                return it.getInetAddresses().find {
                                    it instanceof Inet4Address &&
                                            !it.isLoopbackAddress()
                                }
                            else
                                return null
                        }
            }
            buildConfigField "String","BASE_URL","\"http:/${getDevHost()}:3000/\""
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String","BASE_URL",'"https://adalbadal.com/"'
            signingConfig signingConfigs.release
        }
    }
}


dependencies {
    implementation project(':catloadinglibrary')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.jakewharton:butterknife:10.2.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.3'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'android.arch.lifecycle:viewmodel:1.1.1'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation 'com.github.takusemba:cropme:2.0.5'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.firebase:firebase-messaging:20.2.4'
    implementation 'com.google.firebase:firebase-analytics:17.5.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.1'
    implementation 'com.google.android.libraries.places:places:2.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "org.java-websocket:Java-WebSocket:1.3.0"
}
